#
# JBoss, Home of Professional Open Source.
# Copyright 2011, Red Hat, Inc., and individual contributors
# as indicated by the @author tags. See the copyright.txt file in the
# distribution for a full listing of individual contributors.
#
# This is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this software; if not, write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
# 02110-1301 USA, or see the FSF site: http://www.fsf.org.
#

RULE service B on commitInstallation
CLASS org.jboss.msc.service.ServiceControllerImpl
METHOD commitInstallation
AT SYNCHRONIZE 2
BIND NOTHING
IF incrementCounter("run rule only once for service B on COMMITTING INSTALLATION") == 1
DO
   # hold service installation at commitInstallation
   debug("wait for service A installation"),
   signalWake("service B committing installation", true),
   waitFor("service A INSTALLED", 100000),
   debug("proceed with service B commit installation")
ENDRULE

RULE before service A installation
CLASS org.jboss.msc.service.ServiceContainerImpl
METHOD install
AT ENTRY
BIND NOTHING
IF $1.serviceName.getSimpleName().equals("A")
DO
   # wait for service B to start committing installation
   debug("before service A installation"),
   waitFor("service B committing installation", 100000),
   debug("proceed with service A installation")
ENDRULE

RULE after service A installation
CLASS org.jboss.msc.racecondition.InstallDependentDuringDependencyInstallationTestCase$InstallServiceA
METHOD run
AT EXIT
BIND NOTHING
IF TRUE
DO
    # when InstallServiceA task exits, wake service B installation
    debug("signaling service A INSTALLED"),
    signalWake("service A INSTALLED", true),
    debug("signaled service A INSTALLED")
ENDRULE