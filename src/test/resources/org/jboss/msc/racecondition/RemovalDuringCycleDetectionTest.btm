#
# JBoss, Home of Professional Open Source.
# Copyright 2011, Red Hat, Inc., and individual contributors
# as indicated by the @author tags. See the copyright.txt file in the
# distribution for a full listing of individual contributors.
#
# This is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this software; if not, write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
# 02110-1301 USA, or see the FSF site: http://www.fsf.org.
#
RULE trigger serviceA installation
CLASS org.jboss.msc.racecondition.RemovalDuringCycleDetectionTest$InstallServiceA
METHOD run
AT ENTRY
IF TRUE
DO
   debug("wait for service B about to be removed"),
   waitFor("service B about to be removed", 100000),
   debug ("proceeding with service A installation")
ENDRULE

RULE visit service B on detectCircularity
CLASS org.jboss.msc.service.ServiceContainerImpl
METHOD detectCircularity(org.jboss.msc.service.IdentityHashSet, org.jboss.msc.service.ServiceControllerImpl, java.util.Set, java.util.Deque)
#(org.jboss.msc.service.ServiceControllerImpl)
AT SYNCHRONIZE 1
BIND instanceName = $2.primaryRegistration.name.getSimpleName();
     currentName = $controller.primaryRegistration.name.getSimpleName()
# first attempt will occur on service B installation
IF instanceName.equals("A") AND currentName.equals("B") AND incrementCounter("service B on detectCircularity") == 1
DO
   # hold cycle detection for A installation once it reaches service B in the traversal
   debug("wait for service B removal"),
   signalWake("service B on detectCircularity", true),
   waitFor("service B removed", 100000),
   debug("proceed with service B on detectCircularity")
ENDRULE

RULE before service B removal
CLASS org.jboss.msc.service.ServiceRegistrationImpl
METHOD clearInstance
AT ENTRY
BIND NOTHING
IF $0.name.getSimpleName().equals("B")
DO
   # wait for service B be traversed by cycle detection before removing it
   signalWake("service B about to be removed", true),
   debug("before service B removal"),
   waitFor("service B on detectCircularity", 100000),
   debug("proceed with service B removal")
ENDRULE

RULE after service B removal
CLASS org.jboss.msc.service.ServiceRegistrationImpl
METHOD clearInstance
AT EXIT
BIND NOTHING
IF $0.name.getSimpleName().equals("B")
DO
    # after service B is removed from the ServiceRegistrationImpl, wake cycle detection
    debug("signalling service B removed"),
    signalWake("service B removed", true),
    debug("signalled service B removed")
ENDRULE

